// Artifactory server instance, defined in Jenkins --> Manage:
def artifactory_server = 'demo-arti'

// Conan repository in Artifactory server
def artifactory_repo = 'demo-conan-local'

// User/Pass for Artifactory server
def user_name = 'admin'
def user_apikey = 'APBG9f7okturGUX3EH6LhxhbBmR'

// Source Code project in github
def repo_url = 'https://github.com/xingao0803/demo-multi-builds.git'
def repo_branch = 'master'

// User/Channel for conan package
def conan_user = 'jfrog'
def conan_channel = 'stable'

node {
    def server
    def client
    def serverName
    def buildInfo

    stage("Get project"){
        // Clone source code from github:
        git branch: repo_branch, url: repo_url
    }
    
    stage("Configure Artifactory/Conan"){
        // Obtain an Artifactory server instance: 
        server = Artifactory.server artifactory_server

        // Create a conan client instance:
        client = Artifactory.newConanClient()
 
        // Add a new repository to the conan client.
        // The 'remote.add' method returns a 'serverName' string, which is used later in the script:
        serverName = client.remote.add server: server, repo: artifactory_repo
        
        // Create a local build-info instance:
        buildInfo = Artifactory.newBuildInfo()

        // Login the new conan remote server
        def command = "user -r ${serverName} -p ${user_apikey} ${user_name}"
        client.run(command: command.toString(), buildInfo: buildInfo)
    }

    stage("Generate and upload initial packages"){
        def recipes = ["A", "B", "C", "D", "F", "G" ]
        for ( recipe in recipes ) {
            dir("./recipes/${recipe}") {
                // Generate recipe and 64bits package
                def command = "create . ${conan_user}/${conan_channel} -r ${serverName} -pr ../../profiles/64bits"
                client.run(command: command.toString(), buildInfo: buildInfo)
                
                // Generate recipe and 32bits package
                command = "create . ${conan_user}/${conan_channel} -r ${serverName} -pr ../../profiles/32bits"
                client.run(command: command.toString(), buildInfo: buildInfo)
                
                // Upload recipe and all packages
                command = "upload LIB_${recipe}/*@${conan_user}/${conan_channel} -r ${serverName} -c --all"
                client.run(command: command.toString(), buildInfo: buildInfo)
            }
        }
    }

    stage("Publish buildinfo"){
        // Publish buildinfo to Artifactory repository
        server.publishBuildInfo buildInfo
    }

}
